#
# A theme based on steeef theme
#  * NVM info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#

function prompt_tommydunn_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get node information
  if (( $+functions[node-info] )); then
    node-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_tommydunn_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_tommydunn_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_TOMMYDUNN_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_TOMMYDUNN_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # git
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_TOMMYDUNN_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_TOMMYDUNN_COLORS[5]}‚óè%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_TOMMYDUNN_COLORS[2]}‚óè%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_TOMMYDUNN_COLORS[4]}‚óè%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_TOMMYDUNN_COLORS[3]}‚óè%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format 'üíé %v'

  # node info (nvm)
  zstyle ':prezto:module:node:info:version' format '‚¨¢ %v'

  PROMPT=" ${__PROMPT_TOMMYDUNN_COLORS[4]}%f ${__PROMPT_TOMMYDUNN_COLORS[5]}%2~%f "'$git_info[prompt]'"$ "
  # RPROMPT='%F{green}${node_info[version]%f %F{red}${ruby_info[version]}'
  RPROMPT='%F{green}${node_info[version]}'
  # RPROMPT='%F{red}${ruby_info[version]}'
}

prompt_tommydunn_setup "$@"
